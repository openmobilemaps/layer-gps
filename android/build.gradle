buildscript {
	ext.readProperty = { paramName -> readPropertyWithDefault(paramName, null) }
	ext.readPropertyWithDefault = { paramName, defaultValue ->
		if (project.hasProperty(paramName)) {
			return project.getProperties().get(paramName)
		} else {
			Properties properties = new Properties()
			if (project.rootProject.file('local.properties').exists()) {
				properties.load(project.rootProject.file('local.properties').newDataInputStream())
			}
			if (properties.getProperty(paramName) != null) {
				return properties.getProperty(paramName)
			} else {
				return defaultValue
			}
		}
	}

	ext{
		agp_version = "8.7.2"
		kotlin_version = "2.0.21"

		ubiqueUrl = System.getenv('UB_ARTIFACTORY_URL_ANDROID') ?: readPropertyWithDefault('ubiqueMavenUrl', '')
		ubiqueUser = System.getenv('UB_ARTIFACTORY_USER') ?: readPropertyWithDefault('ubiqueMavenUser', '')
		ubiquePass = System.getenv('UB_ARTIFACTORY_PASSWORD') ?: readPropertyWithDefault('ubiqueMavenPass', '')
		ubiqueMaven = {
			url = ubiqueUrl
			credentials {
				username = ubiqueUser
				password = ubiquePass
			}
			authentication {
				basic(BasicAuthentication)
				digest(DigestAuthentication)
			}
		}
	}
	repositories {
		google()
		maven ubiqueMaven
		mavenCentral()
	}
	dependencies {
		classpath "com.android.tools.build:gradle:$agp_version"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

		classpath 'com.vanniktech:gradle-maven-publish-plugin:0.30.0'
		classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.9.10'
	}
}

repositories {
	google()
	maven ubiqueMaven
	mavenCentral()
}

apply plugin : 'com.android.library'
apply plugin : 'kotlin-android'
apply plugin: 'com.vanniktech.maven.publish'

import com.vanniktech.maven.publish.SonatypeHost

android {
	namespace "io.openmobilemaps.gps"

	compileSdkVersion 34

	defaultConfig {
		minSdkVersion 26
		targetSdkVersion 34
		versionCode Integer.parseInt(VERSION_CODE)
		versionName VERSION_NAME

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		consumerProguardFiles "consumer-rules.pro"
		externalNativeBuild {
			cmake {
				arguments "-DANDROID_STL=c++_shared"
				cppFlags "-std=c++17 -frtti -fexceptions -O2"
			}
		}
	}

	externalNativeBuild {
		cmake {
			path "CMakeLists.txt"
		}
	}

	sourceSets {
		main {
			java {
				srcDir 'src/main/java'
				srcDir '../bridging/android/java'
			}
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_11
		targetCompatibility JavaVersion.VERSION_11
	}

	buildFeatures {
		prefab true
		prefabPublishing true
	}

	prefab{
		layergps {
			headers ".cpp_includes"
			name "layergps"
		}
	}

	packagingOptions {
		exclude("**/libmapscore.so")
		exclude('**/libc++_shared.so')
	}

	defaultPublishConfig "release"

	buildTypes {
		release {
			ndk {
				debugSymbolLevel 'SYMBOL_TABLE'
			}
		}
	}

}

project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
	compilerOptions.jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_11 // Kotlin 1.8+
	kotlinOptions.jvmTarget = "11" // deprecated since Kotlin 1.8
}

task copyHeaders() {
	String headersDir = ".cpp_includes"
	project.delete(headersDir)
	project.mkdir(headersDir)
	ConfigurableFileCollection collection = project.files(
			"src/main/cpp",
			"../bridging/android/jni",
			"../shared/public",
			"../shared/src")
	project.copy {
		duplicatesStrategy(DuplicatesStrategy.FAIL)
		from collection.asFileTree.getFiles()
		include("**/*.h")
		into project.file(headersDir)
	}
}
preBuild.dependsOn copyHeaders

ext.readProperty = { paramName -> readPropertyWithDefault(paramName, null) }
ext.readPropertyWithDefault = { paramName, defaultValue ->
	if (project.hasProperty(paramName)) {
		return project.getProperties().get(paramName)
	} else {
		Properties properties = new Properties()
		if (project.rootProject.file('local.properties').exists()) {
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
		}
		if (properties.getProperty(paramName) != null) {
			return properties.getProperty(paramName)
		} else {
			return defaultValue
		}
	}
}

publishing {
	repositories {
		maven {
			name = "UbiqueMaven"
			url = ubiqueUrl
			credentials {
				username = ubiqueUser
				password = ubiquePass
			}
		}
	}
}

mavenPublishing {
	publishToMavenCentral(SonatypeHost.DEFAULT, true)
	signAllPublications()
}

dependencies {

	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation 'androidx.core:core-ktx:1.13.1'
	implementation 'com.google.android.gms:play-services-location:21.3.0'

	api 'io.openmobilemaps:mapscore:3.0.0-rc.2'
	implementation "ch.ubique.android:djinni-support-lib:1.0.9"

	testImplementation 'junit:junit:4.13.2'
	androidTestImplementation 'androidx.test.ext:junit:1.2.1'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

clean.doLast {
	project.delete("${projectDir}/build")
	project.delete("${projectDir}/.cxx")
}
